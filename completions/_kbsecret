#compdef kbsecret

typeset -A opt_args

_arguments -C \
  '1:cmd:->cmds' \
  '*:: :->args' \
&& ret=0

case "$state" in
  cmds)
    # All the commands and its descriptions
     local actions; actions=(
      'commands:list available commands'
      'conf:open configuration in an editor'
      'dump-fields:dump the fields of a record'
      'env:access environment records'
      'generator:manage generators'
      'generators:list generators'
      'help:print command-line help'
      'list:list records'
      'login:access login records'
      'new-session:create a new session'
      'new:create a new record'
      'pass:retrieve password from a login record'
      'raw-edit:edit the JSON of a record'
      'rm-session:desconfigure and delete a session'
      'rm:remove a record'
      'sessions:list sessions'
      'stash-file:store a file in a record'
      'todo:control to-do records'
      'types:list known record types'
      'version:display version information'
      '-v:display version information'
      '--version:display version information'
      '--help:print command-line help'
      '-h:print command-line help'
     )

      _describe -t actions 'action' actions && ret=0
  ;;
  args)
    case $line[1] in
      # Command help works on other commands
      (--help|-h|help)
        local flags
        flags=( $(kbsecret commands) )
        _describe -t flags 'flag' flags && ret=0
      ;;
      # Commands that don't accept --introspect-flag
      (--version|-v|commands|conf|help|types|version)
      ;;
      *)
        local flags
        flags=( $(kbsecret $line[1] --introspect-flags) )
        # Commands with additional completable arguments
        case $line[1] in
          new)        flags+=($(kbsecret types)) ;;
          login|pass) flags+=($(kbsecret list -t login)) ;;
          env)        flags+=($(kbsecret list -t env)) ;;
          todo)       flags+=($(kbsecret list -t todo)) ;;
          *)          flags+=($(kbsecret list)) ;;
        esac
        # Additional values for some flags
        case ${line[CURRENT-1]} in
          (-s|--session)
            _values -S , 'sessions' $(kbsecret sessions) && ret=0
          ;;
          (-t|--type)
            _values -S , 'types' $(kbsecret types) && ret=0
          ;;
          (-g|--generator)
            _values -S , 'generators' $(kbsecret generators) && ret=0
          ;;
        esac
        _describe -t flags 'flag' flags && ret=0
      ;;
    esac
  ;;
esac

return 1